{GENBFNT.PAS BY KAMIL KOROLKIEWICZ 2006}
uses dos;

procedure tohex(v:byte; var str:string);
const
htab:array [0..15] of char=('0','1','2','3','4','5','6','7','8','9','A','B','C','D','E','F');
begin
     str[0]:=#2;
     str[1]:=htab[v shr 4];
     str[2]:=htab[v and 15];
end;

procedure tohexw(v:word; var str:string);
const
htab:array [0..15] of char=('0','1','2','3','4','5','6','7','8','9','A','B','C','D','E','F');
begin
     str[0]:=#4;
     str[1]:=htab[v shr 12];
     str[2]:=htab[(v shr 8) and 15];
     str[3]:=htab[(v shr 4) and 15];
     str[4]:=htab[v and 15];
end;

var
regs:Registers;
s,o:word;
i,j,k:byte;
f:text;
str,ss,so:string;

begin
     regs.ax:=$1130;
     regs.bx:=$0300;
     intr($10,regs);
     s:=regs.es;
     o:=regs.bp;
     assign (f,'biosfont.h');
     rewrite (f);
     tohexw(s,ss);
     tohexw(o,so);
     writeln (f,'#ifndef __BIOSFONT_H');
     writeln (f,'#define __BIOSFONT_H');
     writeln (f,'//BIOSFONT FROM 0x',ss,':0x',so,' GENERATED BY GENBFNT.PAS');
     writeln (f);
     writeln (f,'unsigned char biosfont[2048]=');
     writeln (f,'{');
     for i:=0 to 127 do
     begin
          write (f,' ');
          for j := 0 to 7 do
          begin
               k:=mem[s:o+i*8+j];
               tohex(k,str);
               write (f,'0x',str);
               write (f,',');
          end;
          writeln (f);
     end;
     regs.ax:=$1130;
     regs.bx:=$0400;
     intr($10,regs);
     s:=regs.es;
     o:=regs.bp;
     for i:=0 to 127 do
     begin
          write (f,' ');
          for j := 0 to 7 do
          begin
               k:=mem[s:o+i*8+j];
               tohex(k,str);
               write (f,'0x',str);
               if (i<>127) or (j<>7) then write (f,',');
          end;
          writeln (f);
     end;
     writeln(f,'};');
     writeln (f);
     writeln (f,'#endif');
     close(f);
end.